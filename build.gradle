import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        maven { url "https://a8c-libs.s3.amazonaws.com/android" }
    }
    dependencies {
        classpath 'com.automattic.android:configure:0.6.5'
    }
}

plugins {
    id "com.android.application" apply false
    id "org.jetbrains.kotlin.android" apply false
    id "com.automattic.android.publish-to-s3" apply false
}

allprojects {
    repositories {
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android"
            content {
                includeGroup "org.wordpress"
                includeGroup "org.wordpress.aztec"
            }
        }
        google()
        mavenCentral()
    }
    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }
}

apply plugin: 'com.automattic.android.configure'

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.9.2'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

ext {
    minSdkVersion = 24
    compileSdkVersion = 33
    targetSdkVersion = 33
}

ext {
    gradlePluginVersion = '3.3.1'
    kotlinCoroutinesVersion = '1.6.4'
    tagSoupVersion = '1.2.1'
    glideVersion = '4.10.0'
    picassoVersion = '2.5.2'
    robolectricVersion = '4.9'
    jUnitVersion = '4.12'
    jSoupVersion = '1.11.3'
    wordpressUtilsVersion = '3.5.0'
    espressoVersion = '3.0.1'

    aztecProjectDependency = project.hasProperty("aztecVersion") ? "org.wordpress:aztec:${project.getProperty("aztecVersion")}" : project(":aztec")
}
